import org.apache.tools.ant.filters.ReplaceTokens
import org.codehaus.groovy.runtime.GStringImpl

plugins {
  id "org.sonarqube" version "2.7.1"
  id 'net.saliman.properties' version '1.4.6'
}

project.ext {
    environment = getEnvProperty('env', 'local')
    bundleDir = getEnvProperty('liferay.workspace.home.dir', project.projectDir.toString() + '/bundles')
    liferayData = project.projectDir.toString() + '/docker/liferay/data'
    liferayMount = project.projectDir.toString() + '/docker/liferay/mount/files'
    dockerContainerPrefix = getEnvProperty('liferay.docker.container.prefix', 'oss')
    dockerComposeFilePath = getEnvProperty('liferay.docker.compose.path', project.projectDir.toString() + '/docker/docker-compose.yml')
    deployFolder = "${bundleDir}/deploy" as GStringImpl
    dbName = getEnvProperty('dbName', 'liferay')
    dbUser = getEnvProperty('dbUser', 'liferay')
    dbPassword = getEnvProperty('dbPassword', 'liferay')
    defaultLiferayAdminPassword = getEnvProperty('default.admin.password', 'test')
    bundleDirectories = [
            file("$bundleDir/deploy"),
            file("$bundleDir/osgi/deploy"),
            file("$bundleDir/osgi/modules"),
            file("$liferayData"),
            file("$liferayMount")
    ]
}

task startContainers(
        description: 'Start all docker containers',
        group: 'docker'
) {
    doLast {
        exec {
            executable 'docker-compose'
            args('-p', dockerContainerPrefix, '-f', dockerComposeFilePath, 'up', '--build', '--force-recreate', '-d')
            environment('LIFERAY_BUNDLE_DIR', bundleDir)
        }
    }
}

task startLiferay(
        'description': 'Start Liferay stack',
        'group': 'docker'
) {
    doLast {
        delete "$bundleDir"
        delete "$liferayMount"
        bundleDirectories.forEach {
            if (!it.exists()) {
                it.mkdirs()
            }
        }
        copy {
            from("$projectDir/configs/$environment/")
            into("$liferayMount")
            include("*.properties")
            filter(ReplaceTokens, tokens: [
                    'MYSQL_DATABASE'        : dbName,
                    'MYSQL_USER'            : dbUser,
                    'MYSQL_PASSWORD'        : dbPassword,
                    'DEFAULT_ADMIN_PASSWORD': defaultLiferayAdminPassword
            ])
        }
        copy {
            from("$projectDir/configs/$environment/") {
                exclude "osgi", "**.properties"
            }
            into("$liferayMount")
        }
        copy {
            from("$projectDir/configs/common/")
            into("$liferayMount")
            include "**/**"
        }
        copy {
            from("$projectDir/configs/$environment/osgi/configs/")
            into("$liferayMount/osgi/configs")
            include "*.config"
        }
        copy {
            from projectDir
            into deployFolder
            include "license.xml"
        }
    }
    finalizedBy startContainers
}

task stopLiferay(
        'description': 'Stop all containers',
        'group': 'docker'
) {
    doLast {
        exec {
            executable 'docker-compose'
            args('-p', dockerContainerPrefix, '-f', dockerComposeFilePath, 'down', '--rmi', 'local')
        }
        print "stop"
    }
}

task dumpDB(
        description: 'Dump the database into the file system.',
        group: 'docker'
) {
    doLast {
        exec {
            executable 'docker'
            args('exec', '-t', "${dockerContainerPrefix}-mariadb", 'mysqldump', "-u${dbUser}", "-p${dbPassword}",
                    '--extended-insert=FALSE', '--no-autocommit', '--opt', "${dbName}")
            standardOutput new FileOutputStream("${projectDir}/docker/mariadb/docker-entrypoint-initdb.d/${dbName}.sql")
        }
        exec {
            executable 'bash'
            args('-c', "sed -i'' -e '1d' ${projectDir}/docker/mariadb/docker-entrypoint-initdb.d/${dbName}.sql")
        }
        delete("${projectDir}/docker/mariadb/docker-entrypoint-initdb.d/${dbName}.sql-e")
    }
}

task geoipDownload{
    group = 'Deltares'
    description = 'Downloads GeoIp2-Lite database'

    doLast {
        def bundlePath = ('/liferay/home/geoip2/')
        def geoLiteTar = ('GeoLite2-City.tar.gz')

        def f = new File(bundlePath + geoLiteTar)

        if (!f.exists()) {
            new URL('https://geolite.maxmind.com/download/geoip/database/' + geoLiteTar).withInputStream{ i -> f.withOutputStream{ it << i }}
        }

        copy {
            from (tarTree(resources.gzip(bundlePath + geoLiteTar))) {
                include "**/*.mmdb"
                eachFile { fcd ->
                    fcd.relativePath = new RelativePath(true, fcd.relativePath.segments.drop(1))
                }
                includeEmptyDirs = false
            }
            into bundlePath
        }
    }
}

def getEnvProperty(String propertyName, String defaultValue) {
    if (project.hasProperty(propertyName)) {
        return project.getProperty(propertyName) ?: defaultValue
    }
    return defaultValue
}
